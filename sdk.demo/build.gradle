//==================================================================================================
// PROJECT
//==================================================================================================
buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0'
        // FABRIC
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'com.google.gms:google-services:3.0.0'
    }
}
task projectInfo << {
    println ""
    println " === ${project.group}:${project.name}:${project.version} - (${System.getenv("CIRCLE_BUILD_NUM")}) ==="
    println ""
}
//==================================================================================================
// ANDROID
//==================================================================================================
apply plugin: 'com.android.application'
android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        applicationId "net.pubnative.sdk.demo"
        versionCode System.getenv("CIRCLE_BUILD_NUM") ? System.getenv("CIRCLE_BUILD_NUM").toInteger() : 1
        versionName project.version
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

repositories {
    mavenLocal()
    jcenter()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':sdk')
    compile project(':sdk.adapter.flurry')
    compile project(':sdk.adapter.facebook')
    compile project(':sdk.adapter.admob')
    compile 'com.github.rtugeek:ColorSeekBar:1.1.2'
    compile 'com.android.support:recyclerview-v7:23.1.0'
    compile 'com.android.support:cardview-v7:23.1.0'
    // FACEBOOK
    compile 'com.facebook.android:audience-network-sdk:4.22.1'
    // FLURRY
    compile 'com.flurry.android:analytics:6.3.1'
    compile 'com.flurry.android:ads:6.3.1'
    //  ADMOB
    compile 'com.google.firebase:firebase-ads:9.6.1'
}
//==================================================================================================
// Google services (ADMOB REQUIRED)
//==================================================================================================
apply plugin: 'com.google.gms.google-services'
//==================================================================================================
// CIRCLE CI
//==================================================================================================
if (System.getenv("CIRCLECI")) {

    //----------------------------------------------------------------------------------------------
    // FABRIC
    //----------------------------------------------------------------------------------------------
    apply plugin: 'io.fabric'
    println ""
    println "======== FABRIC ========"
    try {
        File file = new File("${project.projectDir.absolutePath}/fabric.properties")
        if (!file.canRead()) {
            file.createNewFile()
            file << "apiKey=${System.getenv("FABRIC_API_KEY")}\n"
            file << "apiSecret=${System.getenv("FABRIC_API_SECRET")}\n"
        }
        println " - Success"
        println "========================"
        println ""
    } catch (IOException ioException) {
        println " - Error: "
        println " ${ioException.toString()}"
        println "========================"
        println ""
    }
}


